buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
    }
}

allprojects {
    group "$zkgrailsGroup"
    version "$zkgrailsVersion"
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    it."signing.keyId" = project.hasProperty("signing.keyId") ? project.getProperty('signing.keyId') : System.getenv('SIGNING_KEY_ID')
    it."signing.secretKeyRingFile" = project.hasProperty("signing.secretKeyRingFile") ? project.getProperty('signing.secretKeyRingFile') : "${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg"
    it."signing.password" = project.hasProperty("signing.password") ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSWORD')
}

if (isReleaseVersion) {
    apply plugin: "maven-publish"
    apply plugin: "io.github.gradle-nexus.publish-plugin"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = project.name.replace('-', ' ').capitalize()
                    description = 'A Grails plugin that gives special support for building ZK applications'
                    url = 'https://github.com/zkgroovy/zk-grails'
                    licenses {
                        license {
                            name = 'LGPL 3.0'
                            url = 'https://github.com/zkgroovy/zk-grails/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'maiconandsilva'
                            name = 'Maicon Mauricio'
                        }
                    }
                    scm {
                        url = 'https://github.com/zkgroovy/zk-grails/tree/main'
                        connection = 'scm:git:git://github.com/zkgroovy/zk-grails.git'
                        developerConnection = 'scm:git:ssh://github.com:zkgroovy/zk-grails.git'
                    }
                }
            }
        }
    }

    afterEvaluate {
        signing {
            required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.maven
        }
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }
}

if (isReleaseVersion) {
    nexusPublishing {
        repositories {
            sonatype {
                def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
                def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
                def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
                nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
                snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                username = ossUser
                password = ossPass
                stagingProfileId = ossStagingProfileId
            }
        }
    }
}
